{-------------------------------------------------------------------------------
Tela: frmLogs                                                    Data:08/05/2021
Objetivo: Tela de manutenção dos Logs

Dev.: Sérgio de Siqueira Silva

Data Alteração: 08/05/2021
Dev.: Sérgio de Siqueira Silva
Alteração: Botão excluir todos os logs listados na GRID
-------------------------------------------------------------------------------}

unit Softplan.View.Logs;

interface

uses
  System.SysUtils, System.Types, System.UITypes, System.Classes, System.Variants,
  FMX.Types, FMX.Controls, FMX.Forms, FMX.Graphics, FMX.Dialogs, System.Rtti,
  FMX.Grid.Style, FMX.Controls.Presentation, FMX.ScrollBox, FMX.Grid,
  FMX.Layouts, FMX.Objects, FMX.Edit, FMX.StdCtrls, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS,
  FireDAC.Phys.Intf, FireDAC.DApt.Intf, FireDAC.Stan.Async, FireDAC.DApt,
  Data.DB, FireDAC.Comp.DataSet, FireDAC.Comp.Client, Fmx.Bind.Grid,
  System.Bindings.Outputs, Fmx.Bind.Editors, Data.Bind.EngExt,
  Fmx.Bind.DBEngExt, Data.Bind.Components, Data.Bind.Grid, Data.Bind.DBScope,
  FMX.TabControl, uEnums, Softplan.Controller.Log, Softplan.Model.ConexaoBD,
  FMX.ListBox, FMX.DateTimeCtrls, FMX.Memo, DateUtils;

type
  {Enum com os tipos de busca possiveis}
  TTipoBusca = (tPesqCodigo, tPesqURL, tPesqDataIni, tPesqTodas);

  TfrmLogs = class(TForm)
    LayoutContainer: TLayout;
    LayoutAcoesPesquisa: TLayout;
    Label1: TLabel;
    RectangleEdtNomeFantasia: TRectangle;
    edtPesquisa: TEdit;
    btnPesquisar: TImage;
    LayoutAcoes: TLayout;
    imgExcluir: TImage;
    chkDataIni: TCheckBox;
    chkCodigo: TCheckBox;
    chkURL: TCheckBox;
    TabControl: TTabControl;
    TabListagemLogs: TTabItem;
    TabCadastroLogs: TTabItem;
    LayoutCadastroClientes: TLayout;
    Layout3: TLayout;
    btnVoltar: TImage;
    Grid: TStringGrid;
    StringColumn1: TStringColumn;
    StringColumn2: TStringColumn;
    StringColumn3: TStringColumn;
    StringColumn4: TStringColumn;
    Label7: TLabel;
    Label8: TLabel;
    Label9: TLabel;
    Label10: TLabel;
    Image1: TImage;
    Label15: TLabel;
    btnDetalhar: TRectangle;
    imgDetalhar: TImage;
    Label14: TLabel;
    btnExcluir: TRectangle;
    btExcluirTodos: TRectangle;
    imgExluirTodos: TImage;
    Label2: TLabel;
    eCodigo: TEdit;
    eDataInicial: TEdit;
    eDataFinal: TEdit;
    eURL: TMemo;
    Label3: TLabel;
    eTotalTempo: TEdit;
    procedure edtPesquisaKeyDown(Sender: TObject; var Key: Word;
      var KeyChar: Char; Shift: TShiftState);
    procedure btnPesquisarClick(Sender: TObject);
    procedure btnVoltarClick(Sender: TObject);
    procedure btnDetalharClick(Sender: TObject);
    procedure btnExcluirClick(Sender: TObject);
    procedure chkCodigoClick(Sender: TObject);
    procedure chkURLClick(Sender: TObject);
    procedure chkDataIniClick(Sender: TObject);
    procedure FormDestroy(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure btExcluirTodosClick(Sender: TObject);
    procedure eURLClick(Sender: TObject);
  private
    { Private declarations }
    CtrlLog: TControlLog;
    Query: TFDQuery;
    Conexao: TConexao;

    procedure LimpaGrideDados();
    procedure AbrirURL(const URL: String);
    procedure Checks(const Opcao: Integer);
    procedure CarregarLog(const Codigo: Integer);
    function CodigoSelecionado: Int64;
    procedure LocalizarLog();
    procedure PopularGrid;
    procedure CarregaLogs(TipoBusca:TTipoBusca);
  public
    { Public declarations }
  end;

var
  frmLogs: TfrmLogs;

implementation

{$R *.fmx}

uses {$IFDEF MSWINDOWS}
     Winapi.ShellAPI, Winapi.Windows;
     {$ENDIF MSWINDOWS}

{Função auxiliar para abrir browse padrão do SO}
procedure TfrmLogs.AbrirURL(const URL: String);
begin
  {$IFDEF MSWINDOWS}
  ShellExecute(0, 'OPEN', PChar(URL), '', '', SW_SHOWNORMAL);
  {$ENDIF MSWINDOWS}
end;

{Carregar: Carrega o Controller com Log com os dados e após isso carga dos
           componentes do TabCadastroLog e faz o calculo de tempo total}
procedure TfrmLogs.CarregarLog(const Codigo: Integer);
Var HoraInicial: String;
    HoraFinal:   String;
begin
  if not CtrlLog.Acao(tacCarregar, CodigoSelecionado) then exit;

  eCodigo.Text      := CtrlLog.Log.Codigo.ToString;
  eURL.Text         := CtrlLog.Log.URL;

  //Data e Hora Inicial
  if CtrlLog.Log.DataIni <> 0 then
  begin
    eDataInicial.Text := DateTimeToStr(CtrlLog.Log.DataIni);
    HoraInicial       :=  FormatDateTime('hh:mm:ss',CtrlLog.Log.DataIni);
  end else
    eDataInicial.Text := EmptyStr;

  //Data e Hora Final
  if CtrlLog.Log.DataFim <> 0 then
  begin
    eDataFinal.Text := DateTimeToStr(CtrlLog.Log.DataFim);
    HoraFinal       := FormatDateTime('hh:mm:ss',CtrlLog.Log.DataFim);
  end else
    eDataFinal.Text := EmptyStr;

  //Se tiver finalizado o download faz o calculo de tempo total
  if (HoraInicial <> EmptyStr) and (HoraFinal <> EmptyStr) then
  begin
    eTotalTempo.Text := TimetoStr(StrtoTime(HoraInicial) - StrtoTime(HoraFinal));
  end else
  begin
    eTotalTempo.Text := 'Download não finalizado!';
  end;
end;

{Pesquisar: Utiliza a procedure LocalizarLog se o edtPesquisa tiver conteudo
            caso contrario trazer todos os dados}
procedure TfrmLogs.edtPesquisaKeyDown(Sender: TObject;
  var Key: Word; var KeyChar: Char; Shift: TShiftState);
begin
  if Key = vkReturn then
  begin
    if edtPesquisa.Text <> EmptyStr then
      LocalizarLog
    else
      CarregaLogs(tPesqTodas);
  end;
end;

{Função auxiliar para abrir o link do download no browse padrão do SO}
procedure TfrmLogs.eURLClick(Sender: TObject);
begin
  AbrirURL(eURL.Text);
end;

{Criação dos objetos na memoria: Controller, Conexao BD, Query}
procedure TfrmLogs.FormCreate(Sender: TObject);
begin
  CtrlLog := TControlLog.Create;
  Conexao := TConexao.Create;
  Query   := TFDQuery.Create(nil);
  Query.Connection := Conexao.Conn;

  CarregaLogs(tPesqTodas);
  TabControl.TabIndex := 0;
end;

{Libera objetos da memoria}
procedure TfrmLogs.FormDestroy(Sender: TObject);
begin
  FreeAndNil(CtrlLog);
  FreeAndNil(Conexao);
  FreeAndNil(Query);
end;

{Procedure de carga de dados na Query dos LOGS para exibição no GRID}
procedure TfrmLogs.CarregaLogs(TipoBusca:TTipoBusca);
var sSQL: String;
begin
  sSQL := 'SELECT * FROM LOGDOWNLOAD ';

  //Monstagem da instrução SQL
  Query.Active := False;
  Query.SQL.Clear;

  //Tipo de Busca
  case TipoBusca of
    tPesqCodigo:    begin
                      sSQL := sSQL + 'WHERE CODIGO = :BUSCA';
                      Query.SQL.Add(sSQL);
                      try
                        Query.ParamByName('BUSCA').AsLargeInt := edtPesquisa.Text.ToInt64;
                      except
                        ShowMessage('Código inválido!');
                        exit;
                      end;
                    end;

    tPesqURL:       begin
                      sSQL := sSQL + 'WHERE UPPER(URL) LIKE :BUSCA';
                      Query.SQL.Add(sSQL);
                      Query.ParamByName('BUSCA').AsString := '%' + UpperCase(edtPesquisa.Text) + '%';
                    end;

    tPesqDataIni:   begin
                      sSQL := sSQL + 'WHERE DATE(DATAINICIO) = :BUSCA';
                      Query.SQL.Add(sSQL);
                      try
                        Query.ParamByName('BUSCA').AsDate := StrToDate(edtPesquisa.Text);
                      except
                        ShowMessage('Data inválida!');
                        exit;
                      end;
                    end;

    tPesqTodas:     begin
                      Query.SQL.Add(sSQL);
                    end;

  end;

  Query.Active := True;
  Query.FetchAll;

  PopularGrid;
end;

{Função auxiliar para controle dos chekbox}
procedure TfrmLogs.Checks(const Opcao: Integer);
begin
  case Opcao of
    1 : begin
          chkURL.IsChecked     := False;
          chkDataIni.IsChecked := False;
        end;
    2 : begin
          chkCodigo.IsChecked  := False;
          chkDataIni.IsChecked := False;
        end;
    3 : begin
          chkCodigo.IsChecked  := False;
          chkURL.IsChecked     := False;
        end;
  end;
end;

procedure TfrmLogs.chkCodigoClick(Sender: TObject);
begin
  Checks(1);
end;

procedure TfrmLogs.chkURLClick(Sender: TObject);
begin
  Checks(2);
end;

procedure TfrmLogs.chkDataIniClick(Sender: TObject);
begin
  Checks(3);
end;

{Função auxiliar só pra pegar o código selecionado no GRID}
function TfrmLogs.CodigoSelecionado: Int64;
begin
  try
    Result := Grid.Cells[0,Grid.Row].ToInt64;
  except
    raise Exception.Create('Selecione pelo menos um Log!');
  end;
end;

{Excluir: Primeiro utiliza a objeto de controle do log para localizar o Log
          depois confirma se o usuario deseja realmente excluir}
procedure TfrmLogs.btnExcluirClick(Sender: TObject);
begin
  if Grid.RowCount = 0 then
  begin
    ShowMessage('Não há o que excluir!');
    exit;
  end;

  CtrlLog.Acao(tacCarregar, CodigoSelecionado);
  if MessageDlg('Deseja realmente apagar o log '+ CtrlLog.Log.Codigo.ToString +' ?', System.UITypes.TMsgDlgType.mtConfirmation,
             [System.UITypes.TMsgDlgBtn.mbYes, System.UITypes.TMsgDlgBtn.mbNO], 0) = mrNo then exit;

  CtrlLog.Acao(tacExcluir);

  CarregaLogs(tPesqTodas);
end;

{Pesquisar: Utiliza a procedure LocalizarLog se o edtPesquisa tiver conteudo
            caso contrario trazer todos os dados}
procedure TfrmLogs.btnPesquisarClick(Sender: TObject);
begin
  if edtPesquisa.Text <> EmptyStr then
    LocalizarLog
  else
    CarregaLogs(tPesqTodas);
end;

{Excluir Tudo: Gera um array com todos os códigos que estão na GRID e apaga
         através do Controller do Log}
procedure TfrmLogs.btExcluirTodosClick(Sender: TObject);
Var aCodigos:  Array of Int64;
    iAux:      Integer;
begin
  if Grid.RowCount = 0 then
  begin
    ShowMessage('Não há o que excluir!');
    exit;
  end;

  aCodigos := nil;

  if MessageDlg('Deseja realmente apagar todos os logs da listagem?', System.UITypes.TMsgDlgType.mtConfirmation,
             [System.UITypes.TMsgDlgBtn.mbYes, System.UITypes.TMsgDlgBtn.mbNO], 0) = mrNo then exit;

   SetLength(aCodigos,Grid.RowCount);
   for iAux := 0 to Grid.RowCount-1 do
   begin
     aCodigos[iAux] := Grid.Cells[0,iAux].ToInt64;
   end;

   for iAux := Low(aCodigos) to High(aCodigos) do
   begin
    CtrlLog.Acao(tacCarregar,aCodigos[iAux]);
    CtrlLog.Acao(tacExcluir);
   end;

   CarregaLogs(tPesqTodas);
end;

{Carregar: Consulta detalhada com a possibilidade de abrir o link do download
           no navegador padrão}
procedure TfrmLogs.btnDetalharClick(Sender: TObject);
begin
  if Grid.RowCount = 0 then
  begin
    ShowMessage('Não há logs!');
    exit;
  end;

  CarregarLog(CodigoSelecionado);
  TabControl.Next();
end;

{Voltar: Volta para a listagem de downloads}
procedure TfrmLogs.btnVoltarClick(Sender: TObject);
begin
  TabControl.Previous();
end;

{Limpeza do Grid}
procedure TfrmLogs.LimpaGrideDados;
begin
  Grid.RowCount := 0;
end;

{Procedure auxiliar para pesquisas que precisam usar os Checkbox}
procedure TfrmLogs.LocalizarLog;
Var TesteData: TDate;
begin
  if (chkCodigo.IsChecked) then
  begin
    CarregaLogs(tPesqCodigo);
  end;

  if (chkURL.IsChecked) then
  begin
    CarregaLogs(tPesqURL);
  end;

  if (chkDataIni.IsChecked) then
  begin
    CarregaLogs(tPesqDataIni);
  end;
end;

{Procedure para popular a GRID com os dados carregados na Query pela procedure
 CarregaLogs}
procedure TfrmLogs.PopularGrid;
var iAux: Integer;
begin
  LimpaGrideDados;

  with Query do
  begin

    First;
    Grid.RowCount := RecordCount;
    for iAux := 0 to RecordCount - 1 do
    begin
      Grid.Cells[0,iAux] := FieldByName('CODIGO').AsString;
      Grid.Cells[1,iAux] := FieldByName('URL').AsString;

      if FieldByName('DATAINICIO').AsDateTime <> 0 then
        Grid.Cells[2,iAux] := DateTimeToStr(FieldByName('DATAINICIO').AsDateTime)
      else
        Grid.Cells[2,iAux] := EmptyStr;

      if FieldByName('DATAFIM').AsDateTime <> 0 then
        Grid.Cells[3,iAux] := DateTimeToStr(FieldByName('DATAFIM').AsDateTime)
      else
        Grid.Cells[3,iAux] := EmptyStr;
      Next;
    end;

  end;
end;

end.
